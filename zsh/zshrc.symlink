export DOTFILES=$HOME/.dotfiles
export ZSH=$DOTFILES/zsh

# display how long all tasks over 10 seconds take
export REPORTTIME=10

[[ -e ~/.terminfo ]] && export TERMINFO_DIRS=~/.terminfo:/usr/share/terminfo

# define the code directory
# This is where my code exists and where I want the `c` autocomplete to work from exclusively
if [[ -d ~/code ]]; then
    export CODE_DIR=~/code
fi

# source all .zsh files inside of the zsh/ directory
for config ($ZSH/**/*.zsh) source $config

if [[ -a ~/.localrc ]]; then
    source ~/.localrc
fi


# initialize autocomplete
autoload -U compinit add-zsh-hook
compinit

for config ($ZSH/**/*completion.sh) source $config

export EDITOR='nvim'

export PATH=/usr/local/bin:~/bin:$PATH

# add /usr/local/sbin
if [[ -d /usr/local/sbin ]]; then
    export PATH=/usr/local/sbin:$PATH
fi

# adding path directory for custom scripts
export PATH=$DOTFILES/bin:$PATH

# check for custom bin directory and add to path
if [[ -d ~/bin ]]; then
    export PATH=~/bin:$PATH
fi

[ -z "$TMUX" ] && export TERM=xterm-256color

# install rbenv
if hash rbenv 2>/dev/null; then
    eval "$(rbenv init -)"
fi

if [[ -d ~/.rvm ]]; then
    PATH=$HOME/.rvm/bin:$PATH # Add RVM to PATH for scripting
    source ~/.rvm/scripts/rvm
fi

# alias git to hub
if hash hub 2>/dev/null; then
    eval "$(hub alias -s)"
fi

# source nvm
export NVM_DIR=~/.nvm

load-nvmrc() {
    if [[ -f .nvmrc && -r .nvmrc ]]; then
        nvm use
    fi
}
add-zsh-hook chpwd load-nvmrc

if hash brew 2>/dev/null; then
    local _nvm_prefix="$(brew --prefix nvm 2>/dev/null)"
    [[ -n "$_nvm_prefix" && -f "$_nvm_prefix/nvm.sh" ]] && source "$_nvm_prefix/nvm.sh"
    local _z_path="$(brew --prefix)/etc/profile.d/z.sh"
    [[ -f "$_z_path" ]] && source "$_z_path"
    # when sourcing, check and ensure that the correct Node version is set
    load-nvmrc
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

if [ -z ${DOTFILES_LOADED+x} ]; then
    if hash brew 2>/dev/null; then
        brew_prefix=$(brew --prefix)
    elif [[ -d /opt/homebrew ]]; then
        brew_prefix=/opt/homebrew
    else
        brew_prefix=/usr/local
    fi
    # install zsh-syntax-highlighting
    [[ -f "$brew_prefix/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh" ]] && \
        source "$brew_prefix/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh"
    # install zsh autosuggestions
    [[ -f "$brew_prefix/share/zsh-autosuggestions/zsh-autosuggestions.zsh" ]] && \
        source "$brew_prefix/share/zsh-autosuggestions/zsh-autosuggestions.zsh"
fi

DOTFILES_LOADED=1
